
DEMO   ENGLISH   CONFIG (1.0)
  ROOTCAT   S.
  FILES  .
  LEXENTRIES   (DEMO ENGLISH).
  RULES   (DEMO ENGLISH).
  TEMPLATES   (DEMO ENGLISH).
  GOVERNABLERELATIONS    SUBJ OBJ OBJ2 COMP XCOMP OBL OBL-?+.
  SEMANTICFUNCTIONS    ADJUNCT  TOPIC FOCUS POSS STANDARD.
  NONDISTRIBUTIVES    NUM PERS CONJ-FORM.
  EPSILON   e.
OPTIMALITYORDER NOGOOD.

----
DEMO   ENGLISH   RULES (1.0)

   S --> DP: (^ SUBJ)=!;
     	 VP.

   VP --> V: ^=!;
      	  (DP: (^ OBJ)=!).

   DP --> D: (^ SPEC)=!;
      	  NP.

   NP -->  AP*: ! $ (^ ADJ);
           N.

   AP --> A.
      	  
----
DEMO   ENGLISH   TEMPLATES (1.0)

   TRANS(P) = "transitive verb"
	      (^ PRED)='P<(^ SUBJ)(^ OBJ)>'.

   INTRANS(P) = "intransitive verb"
	      (^ PRED)='P<(^ SUBJ)>'.

   V3SG = "third singular verb form"
	  (^ SUBJ NUM)= SG
	  (^ SUBJ PERS)=3
	  @VPRES.

   VPRES = (^ TENSE)=PRES.

----
DEMO ENGLISH LEXICON (1.0)

a         D * (^ PRED)='a' § @(DET some).

every     D * (^ PRED)='every' § @(DET every).

cat       N * (^ PRED)='cat' § @(CN cat).

sleeps 	  V * @(INTRANS SLEEP)
	      @V3SG § @(IV sleep).

dog       N * (^ PRED)='dog' § @(CN dog).

devours   V * @(TRANS DEVOUR)
	      @V3SG § @(TV devour).

big       A * (^ PRED)='big' § @(MOD big).

----
DEMO ENGLISH SEMANTICTEMPLATES (1.0)

DET(P) = P :: [((SPEC ^)_s VAR):e --o ((SPEC ^)_s RESTR):t] --o [[(SPEC ^)_s:e --o X:t] --o X:t].

CN(P) = P :: (^_s VAR):e --o (^_s RESTR):t.

TV(P) = P :: (^ SUBJ)_s:e --o (^ OBJ)_s:e --o ^_s:t.

IV(P) = P :: (^ SUBJ)_s:e --o ^_s:t.

MOD(Q) = \P -> \X -> (and (Q X) (P X)) :: [((ADJ $ ^)_s VAR):e --o ((ADJ $ ^)_s RESTR):t] --o [((ADJ $ ^)_s VAR):e --o ((ADJ $ ^)_s RESTR):t].

----
